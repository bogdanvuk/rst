grammar rs.demsys.rst.Rst

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate rst "http://www.demsys.rs/rst/Rst"
import "http://www.demsys.rs/rst/Bib" as bib

Document:
    section+=Section*
	;

//SectionOrSpace:
//		Section
//	| 	(WS?) LINE_BREAK
//;

Section:
	   TwoLevelBulletList
	|  BulletList
    |  Replacement
    |  Directive
	|  Paragraph
	|  Heading	   
    |  (WS?) LINE_BREAK
;

Heading:
	label=Label?
	LINE_BREAK
	underlineTop=HeadingUnderline?
    name=SimpleText LINE_BREAK
    underline=HeadingUnderline
;

HeadingUnderline:
      UnderlineEqual
    | UnderlineStar
    | UnderlineSemi
    | UnderlineMinus
;

UnderlineEqual:
    (EQUAL EQUAL EQUAL) EQUAL* LINE_BREAK
;

UnderlineStar:
    (STAR STAR STAR) STAR* LINE_BREAK
;

UnderlineSemi:
    (SEMI SEMI SEMI) SEMI* LINE_BREAK
;

UnderlineMinus:
    (MINUS MINUS MINUS) MINUS* LINE_BREAK
;

TwoLevelBulletList:
	(WS?) LINE_BREAK
	item+=TwoLevelBulletLine+
;

BulletList:
	(WS?) LINE_BREAK
	item+=BulletLine+
;

TwoLevelBulletLine:
	(WS?) STAR WS (=> MINUS) WS text=Text LINE_BREAK
	(WS?)
	BEGIN
		item+=BulletLine+
	END
;

BulletLine:
	(WS?) MINUS WS text=Text (=> LINE_BREAK)
;

Paragraph:
	(WS?) LINE_BREAK
    (line+=TextLine)+
;

Directive:
	//	Label
		MathDirective
	|   IncludeDirective
	|	ImageDirective
	|	BibDirective
	|   CodeBlockDirective
	|	RawDirective
	|	OtherDirective
;

Block:
	(WS?) 
    BEGIN
    	options+=DirectiveOption*
       	block+=Section*
    END
;

RawIndentBlock:
	(WS?) 
    BEGIN
       	(line+=RawLine | block+=RawIndentBlock)*
    END
;

RawBlock:
	(WS?) 
    BEGIN
    	options+=DirectiveOption*
       	(line+=RawLine | block+=RawIndentBlock)*
    END
;

RawLine:
	RawText? LINE_BREAK
;

CodeBlockDirective:
	label=Label?
	(WS?) LINE_BREAK
	'.. ' ('code' | 'code-block') '::'
	firstLine=RawText? 
	LINE_BREAK
	block=RawBlock?
;

RawDirective:
	'.. ' ('raw' | 'tabularcolumns') '::'
	firstLine=RawText? 
	LINE_BREAK
	block=RawBlock?
;

ImageDirective:
	label=Label?
	(WS?) LINE_BREAK
    '.. ' ('image' | 'figure') '::' WS?
    picture=FileName WS? LINE_BREAK
    block=Block?
;

BibDirective:
    '.. ' 'bibliography' '::' WS?
    bib=FileName WS? LINE_BREAK
    block=Block?
;

IncludeDirective:
    '.. ' 'include' '::' WS?
    importURI=FileName WS? LINE_BREAK
;

FileName:
	(SIMPLE_WORD | UnderscoreWord | '.' | '/' )+
;

OtherDirective:
    '.. ' name=SIMPLE_WORD '::' 
    firstLine=Text?
    LINE_BREAK
	block=Block?
    ;

DirectiveOption:
	(WS?) COLON name=SIMPLE_WORD COLON (WS value=SimpleText)? LINE_BREAK
;

MathDirective:
	{MathDirective}
	'.. ' 'math' '::' 
    firstLine=Latex?
    LINE_BREAK
    ( (WS?) 
    BEGIN
    	options+=DirectiveOption*
       	block+=LatexLine*
    END)?
;

Replacement:
	'.. ' PIPE name=SIMPLE_WORD PIPE WS 'replace::'
	text+=Literal+
	LINE_BREAK
;

Label:
	(WS?) LINE_BREAK
	'.. ' UNDERSCORE name=SIMPLE_WORD COLON LINE_BREAK
;

TextLine:
     (WS?) (=> first=FirstTextLiteral) (text=Text)? LINE_BREAK     
;

Text:
    items+=Literal+
;

Literal:
    	Reference
    |	SimpleLiteral
    | 	StyledText
    |	Role
    |	ReplacementRef
    |	COLON
    |	L_ANGLE_BRACKET
    |	R_ANGLE_BRACKET
    
;

FirstTextLiteral:
      SIMPLE_WORD
    | StyledText
    | UnderscoreWord
    | ReplacementRef
//    | WS
;

SimpleTextLine:
	SimpleText (=> LINE_BREAK)
;

SimpleText:
//    (=> SIMPLE_WORD) SimpleLiteral*
//    | WS
	SimpleLiteral+
;

SimpleLiteral:
      UnderscoreWord
    | KEYWORDS
    | SIMPLE_WORD
    | SPECIAL_CHAR
    | WS
;

Role:
		MathRole
	|	CiteRole
	| 	OtherRole
;

MathRole:
	COLON 'math' COLON BACKTICK Latex BACKTICK
;

CiteRole:
	COLON 'cite' COLON BACKTICK entry=[bib::Entry|SIMPLE_WORD] BACKTICK
;

OtherRole:
	COLON name=SIMPLE_WORD COLON BACKTICK text=SimpleText BACKTICK
;

ReplacementRef:
	PIPE name=[Replacement|SIMPLE_WORD] PIPE 
;

Reference:
	  ExternalReference
	| LongReference
	| SimpleReference
  ;

SimpleReference
  :  ReferenceName UNDERSCORE (WS | LINE_BREAK)
  ;
    
ReferenceName:
	name=[Heading|SIMPLE_WORD]
;

RefTarget:
	Heading |
	Label
;

LongReference :  
	BACKTICK 
	ref=[RefTarget|SimpleText]
	BACKTICK UNDERSCORE
  ;

ExternalReferenceLiteral:
	SimpleLiteral
	| COLON
;

ExternalReferenceText:
	ExternalReferenceLiteral+
;

ExternalReference:
	BACKTICK
	title=SimpleText
	L_ANGLE_BRACKET
	link=ExternalReferenceText
	R_ANGLE_BRACKET
	BACKTICK UNDERSCORE
;

LatexLine:
     text=Latex LINE_BREAK     
;

Latex:
	LatexLiteral+
;

LatexLiteral:
		SIMPLE_WORD
	| 	SPECIAL_CHAR
	|	UNDERSCORE
	|	STAR
	|	L_ANGLE_BRACKET
	|	R_ANGLE_BRACKET
	|	WS
;

RawText:
	RawLiteral+
;

RawLiteral:
		SIMPLE_WORD
	|	KEYWORDS
	| 	SPECIAL_CHAR
	|	UNDERSCORE
	|	STAR
	|	L_ANGLE_BRACKET
	|	R_ANGLE_BRACKET
	|	PIPE
	|	COLON
	|	WS
;

StyledText:
      Bold
    | Italic
;

Bold
  :  {Bold}
  	 STAR (=>STAR) 
      text=SimpleText 
      STAR STAR
  ;  

Italic
  :  {Italic}
  	STAR text=SimpleText STAR
  ;
  
UnderscoreWord:
	SIMPLE_WORD (UNDERSCORE SIMPLE_WORD)+
;

//terminal
//SIMPLE_WORD:
//    	('a'..'z' | 'A'..'Z' | '0'..'9') 
//    | (('a'..'z' | 'A'..'Z' | '0'..'9') ('a'..'z' | 'A'..'Z' | '0'..'9' | '-' )* ('a'..'z' | 'A'..'Z' | '0'..'9'))
//;

KEYWORDS:
	  'figure'
	| 'code'
;

terminal
SIMPLE_WORD:
	('a'..'z' | 'A'..'Z' | '0'..'9') ('a'..'z' | 'A'..'Z' | '0'..'9' | '-' )*
;

SPECIAL_CHAR:  (
     '!'
    |MINUS  
    |'"'
    |'#'
    |'$'
    |'%'
    |'&'
    |'\''
    |'('
    |')'
    |'+'
    |','
    |'.'
    |'/'
    |SEMI
    |EQUAL
    |'?'
    |'@'
    |'['
    |'\\'
    |']'
    |'^'
    |'{'
    |'}'
    |'~'
    |'...'
    )
  ;
 
terminal EQUAL: '=';  
terminal STAR: '*';
terminal SEMI: ';';
terminal UNDERSCORE :  '_';
terminal BACKTICK :  '`';
terminal PIPE:  '|';
terminal COLON: ':';
terminal MINUS: '-';
terminal L_ANGLE_BRACKET: '<';
terminal R_ANGLE_BRACKET: '>';

terminal LINE_BREAK :  '\r'? '\n';

terminal WS         : (' '|'\t')+;

//terminal ANY_OTHER: .;
//
terminal BEGIN: 'synthetic:BEGIN';
terminal END: 'synthetic:END';
